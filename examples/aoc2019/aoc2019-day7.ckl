# advent of code day 7
# https://adventofcode.com/2019/day/7

def trace = FALSE;

def parse_program(data) split(data, ",") !> map_list(int);
def make_machine(data) <<<program => parse_program(data), ip => 0, halted => FALSE, input => [], output => []>>>;

def mode_pos = 0;
def mode_imm = 1;

def parse_opcode(opcode) do
    def s = string(opcode);
    while length(s) < 5 do s = "0" + s; end;
    [s !> substr(3, 5) !> int(),
     s !> substr(2, 3) !> int(),
     s !> substr(1, 2) !> int(),
     s !> substr(0, 1) !> int()];
end;

def param_resolve(machine, param, mode) do
    if mode == mode_pos then machine->program[machine->program[machine->ip + param]]
    elif mode == mode_imm then machine->program[machine->ip + param]
    else error("unknown param mode " + mode);
end;

def step(machine) do
    def prog = machine->program;
    def [opcode, mode1, mode2, mode3] = parse_opcode(prog[machine->ip]);
    if opcode == 99 then do
        machine->halted = TRUE;
    end elif opcode == 1 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        if trace then println("add " + a + " + " + b + " store in " + prog[machine->ip + 3]);
        prog[prog[machine->ip + 3]] = a + b;
        machine->ip += 4;
    end elif opcode == 2 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        if trace then println("mul " + a + " * " + b + " store in " + prog[machine->ip + 3]);
        prog[prog[machine->ip + 3]] = a * b;
        machine->ip += 4;
    end elif opcode == 3 then do
        def value = first(machine->input);
        machine->input = rest(machine->input);
        prog[prog[machine->ip + 1]] = value;
        if trace then println("inp " + value + " store in " + prog[machine->ip + 1]);
        machine->ip += 2;
    end elif opcode == 4 then do
        def value = param_resolve(machine, 1, mode1);
        machine->output !> append(value);
        if trace then println("out " + value);
        machine->ip += 2;
    end elif opcode == 5 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        if a != 0 then machine->ip = b
        else machine->ip += 3;
        if trace then println("jump-if-true " + a + " to " + b);
    end elif opcode == 6 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        if a == 0 then machine->ip = b
        else machine->ip += 3;
        if trace then println("jump-if-false " + a + " to " + b);
    end elif opcode == 7 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        prog[prog[machine->ip + 3]] = (if a < b then 1 else 0);
        if trace then println("less " + a + " " + b + " store in " + prog[machine->ip + 3]);
        machine->ip += 4;
    end elif opcode == 8 then do
        def a = param_resolve(machine, 1, mode1);
        def b = param_resolve(machine, 2, mode2);
        prog[prog[machine->ip + 3]] = (if a == b then 1 else 0);
        if trace then println("equals " + a + " " + b + " store in " + prog[machine->ip + 3]);
        machine->ip += 4;
    end;
    machine;
end;

def execute(machine) do
    while not machine->halted do machine = step(machine); end;
end;

def set_input(machine, values) do
    machine->input = values;
    machine;
end;

def add_input(machine, value) do
    machine->input !> append(value);
    machine;
end;

def get_output(machine, idx) machine->output[-1 - idx];


# Part 1

def amplify(filename, phase, input) do
    read_file(filename) !>
    make_machine() !>
    set_input([phase, input]) !>
    execute() !>
    get_output(0);
end;

def maxvalue = 0;
for setting in permutations(range(5)) do
    def value = 0;
    value = amplify("aoc2019-day7-input.txt", setting[0], value);
    value = amplify("aoc2019-day7-input.txt", setting[1], value);
    value = amplify("aoc2019-day7-input.txt", setting[2], value);
    value = amplify("aoc2019-day7-input.txt", setting[3], value);
    value = amplify("aoc2019-day7-input.txt", setting[4], value);
    maxvalue = max(maxvalue, value); 
end;
println("part1: " + maxvalue);


# Part 2

def amplifier(filename, phase) do
    read_file(filename) !>
    make_machine() !>
    set_input([phase]);
end;

def wait_for_output(machine) do
    def output_size = length(machine->output);
    while not machine->halted and length(machine->output) == output_size do
        machine = step(machine);
    end;
    machine;
end;

maxvalue = 0;
for setting in permutations(interval(5, 9)) do
    def amp1 = amplifier("aoc2019-day7-input.txt", setting[0]);
    def amp2 = amplifier("aoc2019-day7-input.txt", setting[1]);
    def amp3 = amplifier("aoc2019-day7-input.txt", setting[2]);
    def amp4 = amplifier("aoc2019-day7-input.txt", setting[3]);
    def amp5 = amplifier("aoc2019-day7-input.txt", setting[4]);
    amp1 !> add_input(0);
    while not amp5->halted do
        amp1 = wait_for_output(amp1);
        amp2 !> add_input(amp1 !> get_output(0));
        amp2 = wait_for_output(amp2);
        amp3 !> add_input(amp2 !> get_output(0));
        amp3 = wait_for_output(amp3);
        amp4 !> add_input(amp3 !> get_output(0));
        amp4 = wait_for_output(amp4);
        amp5 !> add_input(amp4 !> get_output(0));
        amp5 = wait_for_output(amp5);
        amp1 !> add_input(amp5 !> get_output(0));
    end;
    def value = amp5 !> get_output(0);
    maxvalue = max(maxvalue, value); 
end;
println("part2: " + maxvalue);
